#
# The Zimbu compiler written in Zimbu
#
# ARRAY module loader
#
# Copyright 2012 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT "../Builtin.zu"
IMPORT "../Declaration.zu"
IMPORT "../DeclStore.zu"
IMPORT "../ModuleType.zu"
IMPORT "../SContext.zu"

MODULE ARRAYLoader @public @items=public
  Declaration moduleDecl

  # Prepare for using the builtin ARRAY module.
  FUNC prepare() Builtin
    # We need the pName of the functions where they are called.
    DeclStore.wantPName("MARRAYModule", "FmapTo")
    DeclStore.wantPName("MARRAYModule", "FkeyMapTo")
    DeclStore.wantPName("MARRAYModule", "FforEach")
    DeclStore.wantPName("MARRAYModule", "FforKeyEach")

    Builtin b = NEW()
    b.moduleName = "ARRAYModule"
    b.fileName = "ARRAYModule.zu"
    b.setup = setup
    RETURN b
  }

  # Invoked when the ARRAY module is used, after it was processed first time.
  PROC setup(Builtin builtin, SContext ctx)
    IF moduleDecl == NIL
      # Add this module to the list of predefined symbols.
      moduleDecl = Builtin.addLibModule("ARRAY", builtin)
      ModuleType mt = moduleDecl.type.<ModuleType>

      # Remove the methods added for docs only, the real ones are defined
      # below or elsewhere.
      FOR k IN ["size", "Size", "ToString", "asString"]
        Declaration d = mt.findMember(k, NIL)
        IF d != NIL
          mt.removeMember(d)
        }
      }
    }
  }
}
