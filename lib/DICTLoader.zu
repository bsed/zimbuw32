#
# The Zimbu compiler written in Zimbu
#
# DICT module loader
#
# Copyright 2012 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT "../Builtin.zu"
IMPORT "../Declaration.zu"
IMPORT "../DeclStore.zu"
IMPORT "../ModuleType.zu"
IMPORT "../SContext.zu"

MODULE DICTLoader @public @items=public
  Declaration moduleDecl

  # Prepare for using the builtin DICT module.
  FUNC prepare() Builtin
    # We need the pName of the functions where they are called.
    DeclStore.wantPName("MDICTModule", "FmapTo")
    DeclStore.wantPName("MDICTModule", "FkeyMapTo")
    DeclStore.wantPName("MDICTModule", "FforEach")
    DeclStore.wantPName("MDICTModule", "FforKeyEach")

    Builtin b = NEW()
    b.moduleName = "DICTModule"
    b.fileName = "DICTModule.zu"
    b.setup = setup
    RETURN b
  }

  # Invoked when the DICT module is used, after it was processed first time.
  PROC setup(Builtin builtin, SContext ctx)
    IF moduleDecl == NIL
      # Add this module to the list of predefined symbols.
      moduleDecl = Builtin.addLibModule("DICT", builtin)
      ModuleType mt = moduleDecl.type.<ModuleType>

      # Remove the methods added for docs only, the real ones are defined
      # below or elsewhere.
      FOR k IN ["size", "Size", "ToString", "asString"]
        Declaration d = mt.findMember(k, NIL)
        IF d != NIL
          mt.removeMember(d)
        }
      }
    }
  }
}
